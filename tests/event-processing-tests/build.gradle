plugins {
    id 'java'
}

version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.cucumber:cucumber-java:7.8.1'
    implementation platform('software.amazon.awssdk:bom:2.17.179')
    implementation 'software.amazon.awssdk:lambda'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:cloudwatchlogs'
    implementation 'software.amazon.awssdk:cloudwatch'
    implementation 'org.json:json:20220924'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:kms'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.8.1'
    testImplementation 'org.junit.platform:junit-platform-suite:1.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

task cucumber(type: Test) {
    useJUnitPlatform ()
    systemProperty("cucumber.execution.parallel.enabled", true)
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    systemProperty("cucumber.plugin", "pretty, json:"+System.getenv("TEST_REPORT_ABSOLUTE_DIR")+"/result.json, junit:"+System.getenv("TEST_REPORT_ABSOLUTE_DIR")+"/result.xml")
    systemProperty("cucumber.filter.tags", "@"+System.getenv("TEST_ENVIRONMENT"))
}

task cleanup(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('uk.gov.di.txma.eventprocessor.Cleanup')
}

cleanup.configure {
    mustRunAfter cucumber
}

tasks {
    test {
        dependsOn assemble, testClasses, cucumber
        if (System.getenv("TEST_ENVIRONMENT") != "staging"){
            dependsOn cleanup
        }
    }
}
